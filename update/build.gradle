apply from: "${rootProject.rootDir}/project_config.gradle"
def isLib = rootProject.ext.isComponentLib
//def isLib = false
if (isLib) {
    apply plugin: 'com.android.library'
    apply from: '../maven_push.gradle'
} else {
    apply plugin: 'com.android.application'
}
android {
    compileSdkVersion rootProject.ext.android.compileSdkVersion
    buildToolsVersion rootProject.ext.android.buildToolsVersion
    defaultConfig {
        minSdkVersion rootProject.ext.android.minSdkVersion
        targetSdkVersion rootProject.ext.android.targetSdkVersion
        versionCode 1
        versionName "1.0.1"

        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
        testInstrumentationRunnerArguments clearPackageData:'true'

    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    testOptions {
        unitTests {
            returnDefaultValues = true
        }
    }

    testOptions.unitTests.all {
        testLogging {
            events 'passed', 'skipped', 'failed', 'standardOut', 'standardError'
            outputs.upToDateWhen { false }
            showStandardStreams = true
        }
    }

    sourceSets {
        main {
            if (!isLib) {
                manifest.srcFile 'src/demo/AndroidManifest.xml'
                // 合并 demo和main中的res
                res.srcDirs  = ['src/demo/res', 'src/main/res']
                java.srcDirs = ['src/demo/java','src/main/java']
            } else {
                manifest.srcFile 'src/main/AndroidManifest.xml'
                res.srcDirs = ['src/main/res']
                java {
                    //排除java/demo文件夹下的所有文件
                    exclude 'src/demo/**'
                }

                resources {
                    exclude 'src/demo/*'
                }
            }
        }
    }

    lintOptions {
        checkReleaseBuilds false
        abortOnError false
    }
}

dependencies {
    implementation fileTree(include: ['*.jar'], dir: 'libs')
    implementation rootProject.ext.dependencies["appcompat-v7"]
    implementation rootProject.ext.dependencies["constraint-layout"]
    testImplementation rootProject.ext.dependencies["junit"]
    androidTestImplementation rootProject.ext.dependencies["test-runner"]
    androidTestImplementation rootProject.ext.dependencies["espresso-core"]
    implementation rootProject.ext.dependencies["ws-http"]
    implementation rootProject.ext.dependencies["permission"]
    testImplementation rootProject.ext.dependencies["mockito"]

}
