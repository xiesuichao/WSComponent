apply from: "${rootProject.rootDir}/project_config.gradle"
def isLib = rootProject.ext.isComponentLib
//def isLib = false
if (isLib) {
    apply plugin: 'com.android.library'
    apply from: '../maven_push.gradle'
} else {
    apply plugin: 'com.android.application'
}
apply plugin:'com.mob.sdk'

// 加载分享配置信息,在使用组件的时候，在app中依然要配置一次，集成步骤具体可参考http://wiki.mob.com/%E5%AE%8C%E6%95%B4%E9%9B%86%E6%88%90%E6%96%87%E6%A1%A3%EF%BC%88gradle%EF%BC%89/
apply from: "sharesdk.gradle"

android {
    compileSdkVersion rootProject.ext.android.compileSdkVersion
    buildToolsVersion rootProject.ext.android.buildToolsVersion
    defaultConfig {
        minSdkVersion rootProject.ext.android.minSdkVersion
        targetSdkVersion rootProject.ext.android.targetSdkVersion
        versionCode 1
        versionName "1.0"

        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"

    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    sourceSets {
        main {
            if (!isLib) {
                manifest.srcFile 'src/demo/AndroidManifest.xml'
                // 合并 demo和main中的res
                res.srcDirs  = ['src/demo/res', 'src/main/res']
                java.srcDirs = ['src/demo/java','src/main/java']
            } else {
                manifest.srcFile 'src/main/AndroidManifest.xml'
                res.srcDirs = ['src/main/res']
                java {
                    //排除java/demo文件夹下的所有文件
                    exclude 'src/demo/**'
                }

                resources {
                    exclude 'src/demo/*'
                }
            }
        }
    }

    lintOptions {
        checkReleaseBuilds false
        abortOnError false
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])

    implementation rootProject.ext.dependencies["appcompat-v7"]
    implementation rootProject.ext.dependencies["constraint-layout"]
    testImplementation rootProject.ext.dependencies["junit"]
    androidTestImplementation rootProject.ext.dependencies["test-runner"]
    androidTestImplementation rootProject.ext.dependencies["espresso-core"]
}


