apply from: "${rootProject.rootDir}/project_config.gradle"
def isLib = rootProject.ext.isComponentLib
//def isLib = false
if (isLib) {
    apply plugin: 'com.android.library'
    apply from: '../maven_push.gradle'
} else {
    apply plugin: 'com.android.application'
}
android {
    compileSdkVersion rootProject.ext.android.compileSdkVersion
    buildToolsVersion rootProject.ext.android.buildToolsVersion
    defaultConfig {
        minSdkVersion rootProject.ext.android.minSdkVersion
        targetSdkVersion rootProject.ext.android.targetSdkVersion
        versionCode 1
        versionName "1.0"

        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"

        javaCompileOptions {
            annotationProcessorOptions {
                arguments = [AROUTER_MODULE_NAME: project.getName()]
            }
        }

    }

    buildTypes {

        debug{
            buildConfigField "String", "BASE_URL", "\"https://demo.wanshare.com/api/v1/\""
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'

        }

        release {
            buildConfigField "String", "BASE_URL", "\"https://demo.wanshare.com/api/v1/\""
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    sourceSets {
        main {
            if (!isLib) {
                manifest.srcFile 'src/demo/AndroidManifest.xml'
                // 合并 demo和main中的res
                res.srcDirs  = ['src/demo/res', 'src/main/res']
                java.srcDirs = ['src/demo/java','src/main/java']
            } else {
                manifest.srcFile 'src/main/AndroidManifest.xml'
                res.srcDirs = ['src/main/res']
                java {
                    //排除java/demo文件夹下的所有文件
                    exclude 'src/demo/**'
                }

                resources {
                    exclude 'src/demo/*'
                }
            }
        }
    }

    lintOptions {
        checkReleaseBuilds false
        abortOnError false
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])

    implementation rootProject.ext.dependencies["appcompat-v7"]
    implementation rootProject.ext.dependencies["constraint-layout"]
    implementation rootProject.ext.dependencies["design"]
    testImplementation rootProject.ext.dependencies["junit"]
    androidTestImplementation rootProject.ext.dependencies["test-runner"]
    androidTestImplementation rootProject.ext.dependencies["espresso-core"]
    implementation rootProject.ext.dependencies["glide"]
    annotationProcessor rootProject.ext.dependencies["glideCompiler"]
    implementation rootProject.ext.dependencies["photoview"]
    annotationProcessor rootProject.ext.dependencies["arouter-compiler"]
    api (rootProject.ext.dependencies["arouter-api"]){
        exclude group: 'com.android.support', module: 'support-v4'
    }

    implementation rootProject.ext.dependencies["ws-permission"]
    implementation rootProject.ext.dependencies["ws-utils"]
    implementation rootProject.ext.dependencies["ws-ui-dialog"]
}
